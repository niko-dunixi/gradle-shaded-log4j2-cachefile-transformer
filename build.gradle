buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
    }
}
plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id 'java'
    id 'java-gradle-plugin'
    id 'idea'
    id 'eclipse'
}

group 'io.paulbaker'
version "${version != 'unspecified' ? version : '1.0-SNAPSHOT'}"

// Setting target compatibility to Java 7 on purpose.
// This is to match the compatibility restrictions of
// the shadow plugin itself which is Java 7+
sourceCompatibility = 1.7
targetCompatibility = 1.7
idea {
    project {
        languageLevel = '1.7'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

repositories {
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

jar.enabled = false

shadowJar {
    classifier = ''
}

dependencies {
    def shadowPluginVersion = '2.0.4'
    implementation "com.github.jengelman.gradle.plugins:shadow:$shadowPluginVersion"
    compile gradleApi()
    def log4jCoreVersion = '2.11.0'
    compile "org.apache.logging.log4j:log4j-core:$log4jCoreVersion"
    // testing
    testCompile "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testCompile "org.junit.jupiter:junit-jupiter-engine:5.2.0"
    testCompile "org.junit.jupiter:junit-jupiter-params:5.2.0"
}

task downloadDependencies(type: Exec) {
    dependsOn configurations.testRuntime
    commandLine 'echo', 'Downloaded all dependencies'
}

tasks.build.dependsOn tasks.shadowJar

wrapper {
    gradleVersion = '4.8'
    distributionType = Wrapper.DistributionType.ALL
}

gradlePlugin {
    plugins {
        shadowTransformerPlugin {
            id = "${project.group}.shade-log4j-transformer"
            version = project.version
            implementationClass = 'io.paulbaker.gradle.shadow.transform.NoopPlugin'
        }
    }
}

pluginBundle {
    website = "https://github.com/paul-nelson-baker/gradle-shaded-log4j-transformer"
    vcsUrl = "git@github.com:paul-nelson-baker/gradle-shaded-log4j-transformer.git"
    description = "Supplimentary transformer for Jar shading. This provides the transformer for properly shading log4j's cache file"
    tags = ['shade', 'log4j', 'transform']

    plugins {
        shadowTransformerPlugin {
            id = "${project.group}.shade-log4j-transformer"
            displayName = 'Shaded Log4j Transformer'
        }
    }
}